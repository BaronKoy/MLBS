// -----------------------------------------------------------------------------
// Simulation of Cage Experiment (Reuter Lab, 2021) â€” Part 1
// Region: 3R:18,515,500-18,526,500 (~11 kb)
// Using fixed DGRP2 lines: 14 FB + 14 MB
// 700 per generation line (50 x each of 14 lines)
// Neutral model with uniform recombination and mutation rates
// Author: Baron Koylass | Date: 2025-10-28| Version 1.11
// -----------------------------------------------------------------------------

initialize() {
    // ----------------- RANDOM SEED -----------------
    setSeed(20251028);  // fixed seed for reproducibility

    // ----------------- WF MODEL -----------------
    initializeSLiMModelType("WF");
    initializeSLiMOptions(nucleotideBased = T);
    initializeTreeSeq();

    // ----------------- ANCESTRAL REFERENCE -----------------
    initializeAncestralNucleotides(
        "/data/home/bty565/simulations/inputs/fasta/GCA_000001215.4_focal11kb.fa"
    );

    // ----------------- MUTATION & RECOMBINATION -----------------
    defineConstant("RHO", 1.71642e-8);

    initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
    initializeMutationTypeNuc("m2", 0.5, "f", 0.0);
    m1.convertToSubstitution = F;
    m2.convertToSubstitution = F;

    initializeGenomicElementType("g1", c(m1, m2), c(1.0, 0.0), mmJukesCantor(0.0));
    initializeGenomicElementType("g2", c(m1, m2), c(0.0, 1.0), mmJukesCantor(0.0));
    initializeGenomicElementType("g3", c(m1, m2), c(1.0, 0.0), mmJukesCantor(0.0));

    initializeGenomicElement(g1, 0, 4998);
    initializeGenomicElement(g2, 4999, 5999);
    initializeGenomicElement(g3, 6000, 10999);

    initializeRecombinationRate(RHO);
}

// --------------------------- POPULATION SETUP -------------------------------
1 early() {
    sim.addSubpop("p1", 700);  // FB
    sim.addSubpop("p2", 700);  // MB
    sim.addSubpop("p3", 700);  // Cage population

    // Load founder genotypes from VCFs
    p1.haplosomes.readHaplosomesFromVCF(
        "/data/home/bty565/simulations/inputs/vcfs/FB/dgrp2_FB.vcf", m1);
    p2.haplosomes.readHaplosomesFromVCF(
        "/data/home/bty565/simulations/inputs/vcfs/MB/dgrp2_MB.vcf", m1);

    catn("Generation 1: DGRP2 VCFs loaded into p1 and p2.");
}

// --------------------------- 9:1 MIXING -------------------------------------
2 early() {
    // p3 receives 90% gametes from p1, 10% from p2
    p3.setMigrationRates(c(p1, p2), c(0.9, 0.1));

    catn("Generation 2: p3 receives 90% gametes from p1, 10% from p2.");
}

// --------------------------- STOP MIGRATION ---------------------------------
3 early() {
    // Stop migration to p3
    p3.setMigrationRates(c(p1, p2), c(0.0, 0.0));
    catn("Generation 3: Migration stopped. p3 reproduces independently.");
}

// -------------------------- OUTPUT & END ------------------------------------
3 late() {
    defineConstant("OUT_PATH", "/data/home/bty565/simulations/outputs/FB/starting_pop_FB.vcf");

    ind = p3.sampleIndividuals(700);
    ind.outputIndividualsToVCF(filePath = OUT_PATH,
                            simplifyNucleotides = T,
                            keepFixed = T);

    catn("--------------------------------------------------");
    catn("Simulation complete.");
    catn("p3 exported to: " + OUT_PATH);
    catn("Random seed used: " + asString(getSeed()));
    catn("--------------------------------------------------");

    sim.simulationFinished();
}
